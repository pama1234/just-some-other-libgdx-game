apply plugin: 'application'

sourceCompatibility = 17
mainClassName = 'pama1234.game.app.server.launcher.ServerLauncher'
eclipse.project.name = appName + '0006-server'

dependencies {
  implementation project(':server-framework')

  // textmate theme support
  api project(':framework0003:framework0003-textmate')

  // incomplete parse and compile util for java
  // added for test purpose
  api 'org.codehaus.janino:janino:3.1.9'
  api 'com.github.javaparser:javaparser-core:3.25.3'

  // full parse and compile util for java
  api 'org.eclipse.jdt:org.eclipse.jdt.core:3.34.0'
  api 'org.eclipse.jdt:ecj:3.34.0'

  // android dex
  api 'com.linkedin.dexmaker:dexmaker:2.28.3'
  api 'com.android.tools:r8:8.0.40'

  api "org.yaml:snakeyaml:$snakeYamlVersion"

  api "javax.vecmath:vecmath:$vecmathVersion"
  api "com.esotericsoftware:kryo:$kryoVersion"

  api project(':framework0001:framework0001-yaml')
}

jar {
  archiveBaseName.set(eclipse.project.name)
  // the duplicatesStrategy matters starting in Gradle 7.0; this setting works.
  duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
  dependsOn configurations.runtimeClasspath
  from {
    configurations.runtimeClasspath.collect {
      it.isDirectory() ? it : zipTree(it)
    }
  }
  // these "exclude" lines remove some unnecessary duplicate files in the output JAR.
  exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
  dependencies {
    exclude('META-INF/INDEX.LIST', 'META-INF/maven/**')
  }
  // setting the manifest makes the JAR runnable.
  manifest {
    attributes 'Main-Class': project.mainClassName
  }
  // this last step may help on some OSes that need extra instruction to make runnable JARs.
  doLast {
    file(archiveFile).setExecutable(true, false)
  }
}

tasks.distTar { duplicatesStrategy = DuplicatesStrategy.EXCLUDE }
tasks.distZip { duplicatesStrategy = DuplicatesStrategy.EXCLUDE }
tasks.jar { duplicatesStrategy = DuplicatesStrategy.EXCLUDE }

// Equivalent to the jar task; here for compatibility with gdx-setup.
task dist(dependsOn: [jar]) { }
