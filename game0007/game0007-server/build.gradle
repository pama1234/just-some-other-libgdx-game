apply plugin: 'application'

apply plugin: 'com.google.protobuf'

sourceCompatibility = 17
mainClassName = 'pama1234.gdx.game.app.server.ServerLauncher'
eclipse.project.name = appName + '-game0007-server'

dependencies {
  implementation project(":server-framework")

  api project(":framework0001:framework0001-localization")
  api project(":framework0001:framework0001-protobuf")
  api project(":framework0001:framework0001-neat")

  api "javax.vecmath:vecmath:$vecmathVersion"
  api "com.esotericsoftware:kryo:$kryoVersion"

  // api "org.jbox2d:jbox2d-library:$jbox2dVersion"
}

jar {
  archiveBaseName.set(appName)
  // the duplicatesStrategy matters starting in Gradle 7.0; this setting works.
  duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
  dependsOn configurations.runtimeClasspath
  from {
    configurations.runtimeClasspath.collect {
      it.isDirectory() ? it : zipTree(it)
    }
  }
  // these "exclude" lines remove some unnecessary duplicate files in the output JAR.
  exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
  dependencies {
    exclude('META-INF/INDEX.LIST', 'META-INF/maven/**')
  }
  // setting the manifest makes the JAR runnable.
  manifest {
    attributes 'Main-Class': project.mainClassName
  }
  // this last step may help on some OSes that need extra instruction to make runnable JARs.
  doLast {
    file(archiveFile).setExecutable(true, false)
  }
}

// Equivalent to the jar task; here for compatibility with gdx-setup.
task dist(dependsOn: [jar]) { }

tasks.distTar { duplicatesStrategy = DuplicatesStrategy.EXCLUDE }
tasks.distZip { duplicatesStrategy = DuplicatesStrategy.EXCLUDE }
tasks.jar { duplicatesStrategy = DuplicatesStrategy.EXCLUDE }

// java.targetCompatibility = JavaVersion.VERSION_17