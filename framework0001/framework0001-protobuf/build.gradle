apply plugin: 'application'
apply plugin: 'maven-publish'

apply plugin: 'com.google.protobuf'

sourceCompatibility = 17
mainClassName = 'pama1234.util.protobuf.TestApp'
eclipse.project.name = appName + '-protobuf'

dependencies {
  api project(':server-framework')

  api "com.google.protobuf:protobuf-java-util:$protobufVersion"
  api "com.google.protobuf:protobuf-java:$protobufVersion"
}

jar {
  archiveBaseName.set(eclipse.project.name)
  // the duplicatesStrategy matters starting in Gradle 7.0; this setting works.
  duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
  dependsOn configurations.runtimeClasspath
  from {
    configurations.runtimeClasspath.collect {
      it.isDirectory() ? it : zipTree(it)
    }
  }
  // these "exclude" lines remove some unnecessary duplicate files in the output JAR.
  exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
  dependencies {
    exclude('META-INF/INDEX.LIST', 'META-INF/maven/**')
  }
  // setting the manifest makes the JAR runnable.
  manifest {
    attributes 'Main-Class': project.mainClassName
  }
  // this last step may help on some OSes that need extra instruction to make runnable JARs.
  doLast {
    file(archiveFile).setExecutable(true, false)
  }
}

// Equivalent to the jar task; here for compatibility with gdx-setup.
task dist(dependsOn: [jar]) { }

publishing {
  publications {
    maven(MavenPublication) {
      groupId = 'pama1234'
      artifactId = 'framework-protobuf'
      version = "0.0.3"
      from components.java

      pom {
        name = 'pama1234-server-framework'
        description = 'protobuf util'
        url = 'https://github.com/pama1234/just-some-other-libgdx-game'
        scm {
          url = 'https://github.com/pama1234/just-some-other-libgdx-game'
        }
      }
    }
  }
}

protobuf {
  protoc {
    // path = "doc/util/proto/protoc"
    artifact = "com.google.protobuf:protoc:$protobufVersion"
  }
}