apply plugin: 'application'
apply plugin: 'maven-publish'

sourceCompatibility = 17
mainClassName = 'pama1234.util.localization.MainTest'
eclipse.project.name = appName + '-localization'

dependencies {
  implementation project(':server-framework')

  // compileOnlyApi 'org.nuiton.i18n:nuiton-i18n:4.1'
  api 'org.yaml:snakeyaml:2.0'
}

jar {
  archiveBaseName.set(appName)
  // the duplicatesStrategy matters starting in Gradle 7.0; this setting works.
  duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
  dependsOn configurations.runtimeClasspath
  from {
    configurations.runtimeClasspath.collect {
      it.isDirectory() ? it : zipTree(it)
    }
  }
  // these "exclude" lines remove some unnecessary duplicate files in the output JAR.
  exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
  dependencies {
    exclude('META-INF/INDEX.LIST', 'META-INF/maven/**')
  }
  // setting the manifest makes the JAR runnable.
  manifest {
    attributes 'Main-Class': project.mainClassName
  }
  // this last step may help on some OSes that need extra instruction to make runnable JARs.
  doLast {
    file(archiveFile).setExecutable(true, false)
  }
}

// Equivalent to the jar task; here for compatibility with gdx-setup.
task dist(dependsOn: [jar]) {
}

publishing {
  publications {
    maven(MavenPublication) {
      groupId = 'pama1234'
      artifactId = 'framework-localization'
      version = "0.0.3"
      from components.java

      pom {
        name = 'pama1234-framework-localization'
        description = 'localization framework with kryo and yaml'
        url = 'https://github.com/pama1234/just-some-other-libgdx-game'
        scm {
          url = 'https://github.com/pama1234/just-some-other-libgdx-game'
        }
      }
    }
  }
}